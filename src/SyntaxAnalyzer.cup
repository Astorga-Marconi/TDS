/**
 * Copyright (C) 2014
 * All rights reserved.
 *
 * File Name: analizerSyntax.cup
 * To Create: cup analizerSyntax.cup
 * Project: Compilador
 * Authors: Astorga Dario - Marconi Pablo
 * Version: 0.1 
 */
import java_cup.runtime.*;
import ir.ast.*;
import ir.semcheck.*;
import ir.CodeGen.*;
import java.util.*;
import error.Error;
import assemblyGenerator.*;
import semanticAnalyzer.*;

action code {:
	SymbolTable table = new SymbolTable();
	Type currentType;
	Type currentMethodRetType;
	TypeCheckVisitor typeVisitor = new TypeCheckVisitor();
	CodeGenVisitor codeGen = new CodeGenVisitor();
	AssemblyGenerator assemblyGen;
	Integer currentInLoop = 0;	// Cada vez que abro un bloque de ciclo lo incremento y cuando el bloque
								// cierra lo decremento. Entonces Si currentInLoop es 0 no se encuentra
								// en un ciclo, sino lo contrario.
	private void endProgram() {
		for (Error e: typeVisitor.getErrors()) {	// Imprimo por pantalla los errores del checkeo de tipos.
				System.out.println(e.toString());
		}

		// PARA VER EL CODIGO INTERMEDIO GENERADO DESCOMENTAR LA SIGUIENTE LINEA:
		//codeGen.printInstrs();
		
		// Generacion de codigo assembler.
		assemblyGen = new AssemblyGenerator(codeGen.getInstrList());
		System.out.println("Generando assembler ...");
		assemblyGen.generateAssembly("codigoAssemblerGenerado");
		System.out.println("Test finalizado");
	}
:}

/* Terminals (tokens obtained by the lexical analyzer) */
terminal BREAK;
terminal CLASS;
terminal CONTINUE;
terminal FOR;
terminal WHILE;
terminal RETURN;
terminal LBRACE, RBRACE, LBRACK, RBRACK, SEMICOLON, LPAREN, RPAREN, COMMA, EXTERNINVK;
terminal Type TVOID;			
terminal String ID;
terminal BinOpType PLUS, MINUS, MULT, DIV, MOD, LT, GT, LTEQ, GTEQ, EQEQ, NOTEQ, ANDAND, NOT, OROR;
terminal AssignOpType PLUSEQ, MINUSEQ, EQ;
terminal Type TINT, TFLOAT, TBOOLEAN;
terminal String INT_LITERAL, FLOAT_LITERAL, BOOL_LITERAL;
terminal String STRING_LITERAL;
terminal String IF,ELSE;

/* Non terminals */
non terminal	program,
				set_decl,
				field_decl,
				id_field_decl,
				method_decl,
				set_field_decl,	
				set_method_decl,
				list_id_field_decl;
			
non terminal    Type type;
non terminal    Literal literal;
non terminal    Block block, _block, method_block;
non terminal 	AssignOpType assign_op;
non terminal 	LinkedList set_statement, list_expr, list_externinvk_arg, list_type_id_method_decl, zero_or_one_list_type_id_method_decl;
non terminal 	Statement statement, method_call_stmt; // zero_or_one_else_block,
non terminal	Expression expr, method_call_expr, externinvk_arg;
non terminal 	Location location;
non terminal 	VarDescriptor type_id_method_decl;
non terminal 	ReturnStmt return_stmt;
non terminal    IfStmt if_stmt;
non terminal 	BinOpExpr binOpExpr;

/* Precedences */
precedence left  EQ, ANDAND, NOT, NOTEQ;
precedence left  OROR;
precedence left  LT, GT, LTEQ, GTEQ, EQEQ;
precedence left  PLUS, MINUS;
precedence left  MULT, DIV, MOD;

/* The grammar rules */
start with program;

program ::= 
		CLASS ID LBRACE {: table.insertNewBlock(); :} set_decl RBRACE {: table.closeBlock(); endProgram(); :}
	|	CLASS ID LBRACE {: table.insertNewBlock(); :} RBRACE {: table.closeBlock(); endProgram();:}
	;

set_decl ::= 
		set_field_decl set_method_decl
	|	set_method_decl
	|	set_field_decl
	;

set_field_decl ::= 
		set_field_decl field_decl
	|	field_decl
	;

field_decl ::= type list_id_field_decl SEMICOLON;

id_field_decl ::= 
		ID:id1
		{:	VarDescriptor newVar = new VarDescriptor(currentType, id1);
			table.insertNewVar(newVar);
			codeGen.initVar(newVar.getLocation());	:}
	|	ID:id1 LBRACK INT_LITERAL:s RBRACK
		{:	ArrayVarDescriptor newArray = new ArrayVarDescriptor(currentType, id1, s);
			table.insertNewArrayVar(newArray);
			codeGen.initArray(newArray.getLocation());	:}
	;

list_id_field_decl ::= list_id_field_decl COMMA id_field_decl | id_field_decl;

method_decl ::= 
		type:t1 ID:id1 LPAREN zero_or_one_list_type_id_method_decl:params
		{:	currentMethodRetType = t1;
			FunctionDescriptor newFunction = new FunctionDescriptor(t1, id1, params);
			table.insertNewMethod(newFunction);
			codeGen.instrMethodLabel(newFunction);
			table.insertNewBlock();
			table.insertParameters(params);	:}
		RPAREN method_block:b
		{:	table.closeBlock();
			currentMethodRetType = null;
			typeVisitor.visit(b);
			codeGen.visit(b);
			codeGen.instrMethodEnd();	:}
	|	TVOID:t1 ID:id1 LPAREN zero_or_one_list_type_id_method_decl:params
		{:	currentMethodRetType = Type.TVOID;
			FunctionDescriptor newFunction = new FunctionDescriptor(t1, id1, params);
			table.insertNewMethod(newFunction);
			codeGen.instrMethodLabel(newFunction);
			table.insertNewBlock();
			table.insertParameters(params);	:}
		RPAREN method_block:b
		{:	table.closeBlock();
			currentMethodRetType = null;
			typeVisitor.visit(b);
			codeGen.visit(b);
			codeGen.instrMethodEnd();	:}
	;

set_method_decl ::= method_decl set_method_decl | method_decl;

zero_or_one_list_type_id_method_decl ::=
		list_type_id_method_decl:l1
		{: RESULT =  l1; :}
	|	/* LAMBDA */
		{: RESULT = new LinkedList<VarDescriptor>(); :}
	;

list_type_id_method_decl ::= 
		type_id_method_decl:d1 COMMA list_type_id_method_decl:l1
		{:	LinkedList<VarDescriptor> dlist = new LinkedList<VarDescriptor>();
			dlist.add(d1);
			dlist.addAll(l1);
			RESULT = dlist; :}
	|	type_id_method_decl:d1
		{:	LinkedList<VarDescriptor> dlist = new LinkedList<VarDescriptor>();
			dlist.add(d1);
			RESULT = dlist; :} 
	;

type_id_method_decl ::= 
		type ID:id1
		{:	VarDescriptor newVar = new VarDescriptor(currentType, id1);
			RESULT = newVar; :};

method_block ::= _block:b1 {:	RESULT = b1;	:};

block ::= {:table.insertNewBlock();:} _block:b1 {:table.closeBlock(); RESULT = b1; :};

_block ::= 
		LBRACE RBRACE
		{:	RESULT = new Block(table.getCurrentLevel(), (new LinkedList<Statement>()));	:}
	|	LBRACE  set_field_decl set_statement:ss RBRACE
		{:	RESULT = new Block(table.getCurrentLevel(), ss);	:}
	|	LBRACE set_statement:ss RBRACE
		{: RESULT = new Block(table.getCurrentLevel(), ss); :} 
	|	LBRACE set_field_decl RBRACE
		{:	LinkedList<Statement> ss = new LinkedList<Statement>();
			RESULT = new Block(table.getCurrentLevel(), ss); :} 
	;

type ::=
		TBOOLEAN
		{:	currentType = Type.TBOOLEAN;
			RESULT = Type.TBOOLEAN;	:}
	|	TFLOAT
		{:	currentType = Type.TFLOAT;
			RESULT = Type.TFLOAT;	:}
	|	TINT
		{:	currentType = Type.TINT;
			RESULT = Type.TINT;	:}
	;

statement ::=
		location:l1 assign_op:ap1 expr:e1 SEMICOLON
		{:	if (l1 != null || ap1 != null || e1 != null) RESULT = new AssignStmt(l1, ap1, e1); :}
	|	method_call_stmt:mc SEMICOLON
		{: RESULT = mc; :}
	|	if_stmt:a 
		{: RESULT = a; :}	
	|	FOR ID:id1 EQ expr:e1 COMMA expr:e2 {:currentInLoop++;:} block:b1
		{:	currentInLoop--;
			if (table.checkVar(id1)) {
				if (table.search(id1).getLocation() != null) {
					VarLocation loc = (VarLocation) table.search(id1).getLocation();
					RESULT = (new ForStmt(loc, e1, e2, b1));
				} else {
					VarLocation loc = new VarLocation(id1, table.search(id1).getType());
					table.search(id1).setLocation(loc);
					RESULT = (new ForStmt(loc, e1, e2, b1));
				}
			} else {
				RESULT = null;
			}	:}
	|	WHILE expr:e {:currentInLoop++;:} block:b
		{:	currentInLoop--;
			RESULT = new WhileStmt(e, (Block)b); :}
	|	return_stmt:rs SEMICOLON 
		{: RESULT = rs; :}
	|	BREAK SEMICOLON
		{:	if (currentInLoop == 0) System.out.println("Error: Break statement must be in a Loop.");
			else RESULT = new BreakStmt();	:}
	|	CONTINUE SEMICOLON
		{:	if (currentInLoop == 0) System.out.println("Error: Continue statement must be in a Loop.");
			else RESULT = new ContinueStmt(); :}
	|	SEMICOLON
		{: RESULT = new SemicolonStmt(); :}
	|	block:b1
		{: RESULT = b1; :}
	;

if_stmt ::=  
		IF:w LPAREN expr:e RPAREN block:b 
    	{: RESULT = new IfStmt(e, (Block)b); :} 
	|	IF:w LPAREN expr:e RPAREN block:b1 ELSE block:b2 
		{: RESULT = new IfStmt(e, (Block)b1, (Block)b2); :}
	;
      
return_stmt ::= 
		RETURN expr:e 
		{:	if (e.getType() != currentMethodRetType) {
				System.out.println("La expresion de retorno no es del mismo tipo del metodo.");
				System.out.println("Tipo encontrado: " + e.getType() + ", tipo requerido: " + currentMethodRetType);
			}
			RESULT = new ReturnStmt(e); :}
	|   RETURN
		{:	if (currentMethodRetType != Type.TVOID)
				System.out.println("La declaracion return debe retornar una expresion de tipo " + currentMethodRetType);
			RESULT = new ReturnStmt(); :}
	;

set_statement ::= 
		statement:s set_statement:ss
		{:	LinkedList<Statement> set_st = new LinkedList<Statement>();
			if (s != null) set_st.add(s);
			set_st.addAll(ss);
			RESULT = set_st; :} 
	|	statement:s
		{:	LinkedList<Statement> set_st = new LinkedList<Statement>();
			if (s != null) set_st.add(s);
			RESULT = set_st; :}
	;

assign_op ::=
		EQ
		{: RESULT = AssignOpType.EQ; :}
	|	PLUSEQ
		{: RESULT = AssignOpType.PLUSEQ; :}
	|	MINUSEQ
		{: RESULT = AssignOpType.MINUSEQ; :}
	;

method_call_stmt ::= 
		ID:id1 LPAREN  RPAREN
		{:	RESULT = (table.checkMethodCall(id1, (new LinkedList<Expression>()) )) ? (new MethodCallStmt(id1, (new LinkedList<Expression>()))) : null;	:}
	|	ID:id1 LPAREN list_expr:le RPAREN
		{:	RESULT = (table.checkMethodCall(id1, le)) ? (new MethodCallStmt(id1, le)) : null;	:}
	|	EXTERNINVK LPAREN STRING_LITERAL:id1 COMMA type:t RPAREN
		{: RESULT = new ExternInvkStmt(id1, t); :}
	|	EXTERNINVK LPAREN STRING_LITERAL:id1 COMMA type:t COMMA list_externinvk_arg:l RPAREN
		{: RESULT = new ExternInvkStmt(id1, t, l); :}
	|	EXTERNINVK LPAREN STRING_LITERAL:id1 COMMA TVOID:t RPAREN
		{: RESULT = new ExternInvkStmt(id1, Type.TVOID); :}
	|	EXTERNINVK LPAREN STRING_LITERAL:id1 COMMA TVOID:t COMMA list_externinvk_arg:l RPAREN
		{: RESULT = new ExternInvkStmt(id1, Type.TVOID, l); :}
	;

method_call_expr ::= 
		ID:id LPAREN  RPAREN
		{:	RESULT = (table.checkMethodCall(id, (new LinkedList<Expression>()) )) ? (new MethodCallExpr(id, (new LinkedList<Expression>()))) : null; :}
	|	ID:id LPAREN list_expr:le RPAREN
		{:	RESULT = (table.checkMethodCall(id, le)) ? (new MethodCallExpr(id, le)) : null; :}
	|	EXTERNINVK LPAREN STRING_LITERAL:sl COMMA type:t RPAREN
		{:	RESULT = new ExternInvkExpr(sl, t); :}
	|	EXTERNINVK LPAREN STRING_LITERAL:sl COMMA type:t COMMA list_externinvk_arg:lea RPAREN
		{:	RESULT = new ExternInvkExpr(sl, t, lea); :}
	|	EXTERNINVK LPAREN STRING_LITERAL:sl COMMA TVOID:tv RPAREN
		{:	RESULT = new ExternInvkExpr(sl, Type.TVOID); :}
	|	EXTERNINVK LPAREN STRING_LITERAL:sl COMMA TVOID:tv COMMA list_externinvk_arg:lea RPAREN
		{:	RESULT = new ExternInvkExpr(sl, Type.TVOID, lea); :}
	;

location ::= 
		ID:id1
		{:	if (table.checkVar(id1)) {
				RESULT = table.search(id1).getLocation();
			} else {
				RESULT = null;
			}	:}
	|	ID:id1 LBRACK expr:e1 RBRACK
		{:	if (table.checkArrayVar(id1)) {
				Location loc = table.search(id1).getLocation();
				if (loc instanceof ArrayLocation) {
					RESULT = (new ArrayLocation((ArrayLocation)loc, e1));
				} else if (loc instanceof GlobalArrayLocation) {
					RESULT = (new GlobalArrayLocation((GlobalArrayLocation)loc, e1));
				}
			} else {
				RESULT = null;
			}	:}
	;

list_expr ::= 
		expr:e1 COMMA list_expr:lexpr
		{:	LinkedList<Expression> list_e = new LinkedList<Expression>();
			list_e.add(e1);
			list_e.addAll(lexpr);
			RESULT = list_e; :}
	|	expr:e1
		{:	LinkedList<Expression> list_e = new LinkedList<Expression>();
			list_e.add(e1);
			RESULT = list_e; :}
	;

expr ::=
		location:l1
		{: RESULT = l1; :}
	|	method_call_expr:m1
		{: RESULT = m1; :}
	|	literal:l1
		{: RESULT = l1; :}
	|	binOpExpr:b1
		{: RESULT = b1; :}
	|	MINUS expr:e1
		{: RESULT = new NegativeExpr(e1); :}
	|	NOT:n1 expr:e1 
		{: RESULT = new NotExpr(n1, e1); :}
	|	LPAREN:l expr:e RPAREN:r 
		{:RESULT = new ParentExpr(e, e.getType());:}
	;

binOpExpr ::=
		expr:e1 PLUS expr:e2
		{:	RESULT = new ArithExpr(e1, BinOpType.PLUS, e2);	:}
	|	expr:e1 MINUS expr:e2
		{:	RESULT = new ArithExpr(e1, BinOpType.MINUS, e2);	:}
	|	expr:e1 MULT expr:e2
		{:	RESULT = new ArithExpr(e1, BinOpType.MULT, e2);	:}
	|	expr:e1 DIV expr:e2
		{:	RESULT = new ArithExpr(e1, BinOpType.DIV, e2);	:}
	|	expr:e1 MOD expr:e2
		{:	RESULT = new ArithExpr(e1, BinOpType.MOD, e2);	:}
	|	expr:e1 LT expr:e2
		{:	RESULT = new RelExpr(e1, BinOpType.LT, e2);	:}
	|	expr:e1 GT expr:e2
		{:	RESULT = new RelExpr(e1, BinOpType.GT, e2);	:}
	|	expr:e1 LTEQ expr:e2
		{:	RESULT = new RelExpr(e1, BinOpType.LTEQ, e2);	:}
	|	expr:e1 GTEQ expr:e2
		{:	RESULT = new RelExpr(e1, BinOpType.GTEQ, e2);	:}
	|	expr:e1 EQEQ expr:e2
		{:	RESULT = new EqExpr(e1, BinOpType.EQEQ, e2);	:}
	|	expr:e1 NOTEQ expr:e2
		{:	RESULT = new EqExpr(e1, BinOpType.NOTEQ, e2);	:}
	|	expr:e1 ANDAND expr:e2
		{:	RESULT = new CondExpr(e1, BinOpType.ANDAND, e2);	:}
	|	expr:e1 OROR expr:e2
		{:	RESULT = new CondExpr(e1, BinOpType.OROR, e2);	:}
	;

externinvk_arg ::=
		expr:e1
		{: RESULT = e1; :}
		//{: RESULT = new ExternInvkArgExpr(e1); :}
	|	STRING_LITERAL:s1
		{: RESULT = new ExternInvkArgStringLit(s1); :}
	;
	
list_externinvk_arg ::= 
		externinvk_arg:ei COMMA list_externinvk_arg:lei
		{:	LinkedList<Expression> list_ei = new LinkedList<Expression>();
			list_ei.add(ei);
			list_ei.addAll(lei);
			RESULT = list_ei; :}
	|	externinvk_arg:e1
		{:	LinkedList<Expression> list_ei = new LinkedList<Expression>();
			list_ei.add(e1);
			RESULT = list_ei; :}
	;

literal ::=
		INT_LITERAL:l
		{:	RESULT = new IntLiteral(l);	:}
	|	FLOAT_LITERAL:l
		{:	RESULT = new FloatLiteral(l); :}
	|	BOOL_LITERAL:l
		{:	RESULT = new BoolLiteral(l); :}
	;

